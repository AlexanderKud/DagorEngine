include "shader_global.dshl"
include "gbuffer.dshl"
include "psh_derivate.dshl"
include "normaldetail.dshl"

texture tex_hmap_low;

int heightmap_scale_offset = 70;
hlsl(vs) {
  float4 heightmap_scale_offset[72]: register(c70);
  float4 lastreg:register(c142);
}

texture heightmap;
float4 world_to_hmap_low = (10e+3, 10e+3, 10e+10, 10e+10);
float4 tex_hmap_inv_sizes = (1/2048,1/2048,0,0);
float4 heightmap_scale = (20,0,0,0);

shader heightmap
{
  (vs)
  {
    tex_hmap_low@smp2d = tex_hmap_low;
    globtm@f44 = globtm;
    world_view_pos@f3 = world_view_pos;
    world_to_hmap_low@f4 = world_to_hmap_low;
    heightmap_scale@f2 = heightmap_scale;
    tex_hmap_inv_sizes@f4 = tex_hmap_inv_sizes;
  }
  no_ablend; // TODO

  channel short2 pos[0]=pos[0];

  //cull_mode = none;

  hlsl(vs) {
    #define decode_height(a) ((a)*heightmap_scale.x+heightmap_scale.y)

## if hardware.dx11 || hardware.ps4
    #define input_used_instance_id HW_input_used_instance_id
    #define used_instance_id HW_used_instance_id
    #define USED_INSTANCE_ID HW_USED_INSTANCE_ID
    #define USE_INSTANCE_ID HW_USE_INSTANCE_ID
## else
    #define input_used_instance_id ,int2 instance_id
    #define used_instance_id ,instance_id
    #define USED_INSTANCE_ID ,instance_id.x
    #define USE_INSTANCE_ID  ,int2 instance_id:TEXCOORD12
##endif
    float2 decodeWorldPosXZ(int2 inPos input_used_instance_id)
    {
      return heightmap_scale_offset[instance_id.x].xy*inPos+heightmap_scale_offset[instance_id.x].zw;
    }
    float getHeight(float2 worldPosXZ)
    {
      float2 tc_low = worldPosXZ*world_to_hmap_low.xy + world_to_hmap_low.zw;
      float height = tex2Dlod(tex_hmap_low, float4(tc_low,0,0)).r;
      float decodedHeight = decode_height(height);
      return decodedHeight;
    }
    float3 getWorldPos(int2 inPos input_used_instance_id)
    {
      float3 worldPos;
      worldPos.xz = decodeWorldPosXZ(inPos used_instance_id);
      //float2 heightmapRegion = float2(10000, 10000);
      //worldPos.xz = clamp(worldPos.xz, -heightmapRegion, heightmapRegion);
      worldPos.y = getHeight(worldPos.xz);
      return worldPos;
    }
  }

  hlsl {
    struct VsOutput
    {
      VS_OUT_POSITION(pos)
      float3 p2e:  TEXCOORD1;
    };
  }
  hlsl(vs) {

    VsOutput test_vs(int2 posXZ : POSITION USE_INSTANCE_ID)
    {
      VsOutput output;
      float3 worldPos = getWorldPos(posXZ USED_INSTANCE_ID);
      output.pos = mul(float4(worldPos, 1), globtm);
      output.p2e = world_view_pos-worldPos;
      return output;
    }
  }
  //cull_mode = none;


  USE_PIXEL_TANGENT_SPACE()
  WRITE_GBUFFER()

  (ps)
  {
    sizes@f4 = (tex_hmap_inv_sizes.x/world_to_hmap_low.x/heightmap_scale.x, 0, 0, 0);
    tex_hmap_inv_sizes@f4 = (tex_hmap_inv_sizes.x, tex_hmap_inv_sizes.y, 1/tex_hmap_inv_sizes.x, 1/tex_hmap_inv_sizes.y);
    world_to_hmap_low@f4 = world_to_hmap_low;
    world_view_pos@f3 = world_view_pos;
    heightmap@smp2d = heightmap;
  }

  USE_NORMAL_DETAIL()
  hlsl(ps) {
    #define sizeInMeters (sizes.x)

    float getTexel(float2 p)
    {
      return h1tex2D(heightmap, p);
    }

    half3 getNormalLow(float2 pos)
    {
      float3 offset = float3(tex_hmap_inv_sizes.x, 0, tex_hmap_inv_sizes.y);
      half W = getTexel(float2(pos.xy - offset.xy));
      half E = getTexel(float2(pos.xy + offset.xy));
      half N = getTexel(float2(pos.xy - offset.yz));
      half S = getTexel(float2(pos.xy + offset.yz));
      return normalize(half3(W-E, sizeInMeters, N-S));
    }
    half3 getWorldNormal(float3 worldPos)
    {
      half3 normal;
      float2 worldPosXZ = worldPos.xz;
      float2 tc_low = worldPosXZ*world_to_hmap_low.xy + world_to_hmap_low.zw;
      normal = getNormalLow(tc_low);
      return normal;
    }

    GBUFFER_OUTPUT test_ps(VsOutput input HW_USE_SCREEN_POS INPUT_VFACE)
    {
      float4 screenpos = GET_SCREEN_POS(input.pos);
      float3 worldPos = world_view_pos.xyz-input.p2e.xyz;
      float3 worldNormal = getWorldNormal(worldPos.xyz).xyz;

      UnpackedGbuffer result;
      init_gbuffer(result);
      float2 worldPosXZ = worldPos.xz;
      float2 tc_low = worldPosXZ*world_to_hmap_low.xy + world_to_hmap_low.zw;

      //texCoord = input.tc;
      half3 albedo = half3(0.15,0.2, 0.05);

      //init_albedo_roughness(result, albedo_roughness);
      init_ao(result, 1);
      init_albedo(result, albedo.xyz);
      init_smoothness(result, 0.01);
      init_normal(result, worldNormal);
      return encode_gbuffer(result, input.pointToEye, screenpos);
    }
  }
  compile("target_vs", "test_vs");
  compile("target_ps", "test_ps");
}